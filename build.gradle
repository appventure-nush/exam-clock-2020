plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group 'app.nush'
version '1.3.6-beta'
mainClassName = 'app.nush.examclock.ExamClock'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'no.tornado:tornadofx-controls:1.0.4'
    implementation 'io.socket:socket.io-client:1.0.0'
    implementation group: 'com.dlsc.preferencesfx', name: 'preferencesfx-core', version: '8.6.0'
}

task generateTestSources {
    inputs.property "version", project.version
    outputs.dir "$buildDir/generated"
    doFirst {
        def versionFile = file("$buildDir/generated/app/nush/examclock/Version.java")
        versionFile.parentFile.mkdirs()
        versionFile.text = """package app.nush.examclock;public class Version {public static String getVersion() {return "$project.version";}}"""
    }
}

compileJava.dependsOn generateTestSources

sourceSets.main.java {
    srcDir "$buildDir/generated"
}
shadowJar.doLast {
    println "Deploying..."
    exec {
        commandLine 'sh', './build.sh'
    }
}
jar {
    manifest {
        attributes 'Main-Class': 'app.nush.examclock.ExamClock'
    }
}